/****************************************************************************
*****************************************************************************
UDO DEFINITIONS IN tables/create:
*****************************************************************************
Sinc       : iFt Sinc iStart, iEnd [,iSize [,iFtNo]]
*****************************************************************************
****************************************************************************/

/****************************************************************************
iFt Sinc iStart, iEnd [,iSize [,iFtNo]]
Creates a table with a sinc function.

Creates a table containing the values of a sinc function f(x)/x from x between start and end, with a table size of 4096 as default. Returns the table number which is by default generated by Csound, but can also be assigned by an input.
written by joachim heintz

iStart - lowest x value for sinc function
iEnd - highest x value for sinc function
iSize - size of the function table generated (default 4096 points)
iFtNo - number of the generated table (default 0: generated by Csound)
iFt - the number of the table 
****************************************************************************/

opcode Sinc, i, iioo
 iStart, iEnd, iSize, iFtNo xin
 iFt ftgen 0, 0, -iSize, 2, 0
 iRange = iEnd - iStart
 iCnt = 0
 while iCnt < iSize do
  iX = iRange/(iSize-1) * iCnt + iStart
  iY = (iX == 0 ? 1 : sin(iX) / iX)
  tablew iY, iCnt, iFt
  iCnt += 1
 od
 xout iFt
endop


